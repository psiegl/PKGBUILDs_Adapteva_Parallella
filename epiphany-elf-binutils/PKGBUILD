# Maintainer: Mateusz Kaczanowski <kaczanowski.mateusz@gmail.com>
# Adapted from aarch64-linux-gnu-binutils by Anatol Pomozov <anatol.pomozov@gmail.com>

_target=epiphany-elf
pkgname=$_target-binutils
pkgver=2.38
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the Adapteva Epiphany target'
arch=(i686 x86_64 armv7h)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(glibc zlib libelf)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.lz{,.sig})
sha1sums=('221e5aa4c42b0c9f47f682a36e7ef31713c22442'
          'SKIP')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F') # Nick Clifton (Chief Binutils Maintainer) <nickc@redhat.com>

build() {
  cd binutils-$pkgver

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=$_target \
              --prefix=/usr \
              --enable-multilib \
              --enable-interwork \
              --with-gnu-as \
              --with-gnu-ld \
              --disable-nls \
	      --disable-werror

  make configure-host
  make
}

check() {
  cd binutils-$pkgver
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/${_target}-{dlltool,windres,windmc}*
  rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
  rmdir "$pkgdir"/usr/lib/bfd-plugins

  # Remove info documents that conflict with host version
  rm -rf "$pkgdir"/usr/share/info
}
